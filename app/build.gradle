apply plugin: 'com.android.application'

android {
    compileSdk 34
    android.buildFeatures.buildConfig true

    defaultConfig {
        applicationId "com.easyfitness"
        minSdkVersion 31
        targetSdkVersion 34
        versionCode 68
        versionName '0.20.7'
        vectorDrawables.useSupportLibrary true
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        if (rootProject.file('release.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('release.properties')))
            release {
                storeFile file(props.keyStore)
                storePassword props.keyStorePassword
                keyAlias props.keyAlias
                keyPassword props.keyAliasPassword
            }
        }
        if (rootProject.file('debug.properties').exists()) {
            def props2 = new Properties()
            props2.load(new FileInputStream(rootProject.file('debug.properties')))
            debug {
                storeFile file(props2.keyStore)
                storePassword props2.keyStorePassword
                keyAlias props2.keyAlias
                keyPassword props2.keyAliasPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("bool", "FIREBASE_CRASH_ENABLED", "true")
            if (rootProject.file('release.properties').exists()) {
                signingConfig signingConfigs.release
            }
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            resValue "string", "app_name", "Fast N Fitness"
        }
        debug {
            resValue("bool", "FIREBASE_CRASH_ENABLED", "false")
            if (rootProject.file('debug.properties').exists()) {
                signingConfig signingConfigs.debug
            }
            buildConfigField "Boolean", "DEBUG_MODE", "true"
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Fast N Fitness Debug"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    namespace 'com.easyfitness'

    lintOptions {
        abortOnError false
        disable 'GoogleAppIndexingWarning'
        baseline file("lint-baseline.xml")
    }
}

dependencies {
    // Support
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation("androidx.swiperefreshlayout:swiperefreshlayout:1.1.0")

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testImplementation 'org.mockito:mockito-core:5.15.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.15.2'

    // Other
    implementation 'com.github.JDevZone:FillProgressLayout:0.0.8'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'
    //implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    implementation 'com.mikhaellopez:circularimageview:4.3.1'
    implementation 'com.github.f0ris.sweetalert:library:1.6.2'
    implementation 'com.github.onurkagan:ktoast:1.0.3'
    implementation 'com.vanniktech:android-image-cropper:4.5.0'
    implementation 'com.github.ivbaranov:materialfavoritebutton:0.1.5'
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
}

